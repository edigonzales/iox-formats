plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

apply from: "$rootDir/gradle/versioning.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'io.github.sogis'

repositories {
    maven { 
        url "https://repo.osgeo.org/repository/release/" 
    } 
    mavenLocal()
    mavenCentral()
    maven {
        url "https://jars.interlis.ch"
    }
}

// Das ist wirklich komisch. Irgendwie kommen sich "core" und "client" in die Quere.
// Jedenfalls führt der Verzicht auf "client" zu massiv wenigeren Dependencies (auch in GRETL).
configurations {
    testImplementation {
        exclude(group: 'org.apache.hadoop', module: 'hadoop-core')
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.apache.hadoop:hadoop-client:3.3.5' // Wegen "HadoopInputFile.fromPath" glaube ich. Beim Writer selber nicht nötig.

    testImplementation 'org.testcontainers:testcontainers:1.17.2'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.2'
    testImplementation 'org.testcontainers:postgresql:1.17.2'
    testImplementation 'org.postgresql:postgresql:42.6.0'

    api 'ch.interlis:iox-wkf:1.1.1'
        
    api 'org.apache.parquet:parquet-hadoop:1.13.1'
    api 'org.apache.hadoop:hadoop-core:1.2.1'
    //api 'org.apache.hadoop:hadoop-client:3.3.5'
    api 'org.apache.parquet:parquet-avro:1.13.1'
    api 'org.apache.avro:avro:1.11.1'
    
    api 'org.apache.poi:poi:5.2.3'
    api 'org.apache.poi:poi-ooxml:5.2.3'
}

tasks.named('test') {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "iox-formats"
                url = "https://github.com/sogis/iox-formats"
                description = "iox-formats"
                scm {
                    connection = "scm:git:https@github.com/sogis/iox-formats"
                    developerConnection = "scm:git:https@github.com/sogis/iox-formats"
                    url = "https://github.com/sogis/iox-formats"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/sogis/iox-formats/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "edigonzales"
                        name = "Stefan Ziegler"
                    }
                }
            }
        }
    }
}

if (!version.toString().endsWith("LOCALBUILD")) {
    signing {
        sign publishing.publications.mavenJava
    }
}

nexusPublishing {
    repositories {
        sonatype { 
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
            password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
        }
    }
}
