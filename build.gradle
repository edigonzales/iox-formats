plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
}

apply from: "$rootDir/gradle/versioning.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'io.github.sogis'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://jars.interlis.ch"
    }
}

// Das ist wirklich komisch. Irgendwie kommen sich "core" und "client" in die Quere.
// Jedenfalls führt der Verzicht auf "client" zu massiv wenigeren Dependencies (auch in GRETL).
configurations {
    testImplementation {
        exclude(group: 'org.apache.hadoop', module: 'hadoop-core')
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.apache.hadoop:hadoop-client:3.3.5' // Wegen "HadoopInputFile.fromPath" glaube ich. Beim Writer nicht nötig.

    api('ch.interlis:iox-ili:1.21.6') {
        exclude group: 'ch.ehi', module: 'ehibasics'
    }
    api 'ch.ehi:ehibasics:1.4.1'
    api 'ch.interlis:ili2c-tool:5.2.2'
    api 'ch.interlis:ili2c-core:5.2.2'
    //api group: 'ch.interlis', name: 'iox-wkf', version: '1.1.0'
        
    api 'org.apache.parquet:parquet-hadoop:1.13.1'
    api 'org.apache.hadoop:hadoop-core:1.2.1'
    //api 'org.apache.hadoop:hadoop-client:3.3.5'
    api 'org.apache.parquet:parquet-avro:1.13.1'
    api 'org.apache.avro:avro:1.11.1'
    
    api 'org.apache.poi:poi:5.2.3'
    api 'org.apache.poi:poi-ooxml:5.2.3'
}

tasks.named('test') {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "iox-parquet"
                url = "https://github.com/edigonzales/iox-parquet"
                description = "iox-parquet"
                scm {
                    connection = "scm:git:https@github.com/edigonzales/iox-parquet"
                    developerConnection = "scm:git:https@github.com/edigonzales/iox-parquet"
                    url = "https://github.com/edigonzales/iox-parquet"
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://github.com/edigonzales/iox-parquet/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "edigonzales"
                        name = "Stefan Ziegler"
                    }
                }
            }
        }
    }
}

if (!version.toString().endsWith("LOCALBUILD")) {
    signing {
        sign publishing.publications.mavenJava
    }
}

nexusPublishing {
    repositories {
        sonatype { 
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
            password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
        }
    }
}
